#version 460

#extension GL_EXT_mesh_shader : require

#extension GL_GOOGLE_include_directive : require
#include "Assets/Shaders/Include/Globals.h"

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;
layout(triangles, max_vertices = 64, max_primitives = 124) out; // 126 optimal nvidia

void main()
{
    const uint mi = gl_WorkGroupID.x; // current work group
    const uint ti = gl_LocalInvocationID.x; // invocation of current work group
    
    const uint primitiveCount = s_GlobalMeshletBuffers[u_PC.MeshletBufferIndex].meshlets[mi].triangleCount;
    const uint vertexCount = s_GlobalMeshletBuffers[u_PC.MeshletBufferIndex].meshlets[mi].vertexCount;
    SetMeshOutputsEXT(vertexCount, primitiveCount);

    for(uint i = ti; i < vertexCount; i+=32)
    {
        const uint vi =  s_GlobalMeshletBuffers[u_PC.MeshletBufferIndex].meshlets[mi].vertices[i];

        gl_MeshVerticesEXT[i].gl_Position = u_GlobalCameraData.Projection * u_GlobalCameraData.InverseView * u_PC.Transform * vec4(s_GlobalVertexPosBuffers[u_PC.VertexPosBufferIndex].Positions[vi].Position, 1.0);
    }

    for(uint i = ti; i < primitiveCount; i+=32)
    {
        gl_PrimitiveTriangleIndicesEXT[i] = uvec3(
            s_GlobalMeshletBuffers[u_PC.MeshletBufferIndex].meshlets[mi].triangles[3*i+0], 
            s_GlobalMeshletBuffers[u_PC.MeshletBufferIndex].meshlets[mi].triangles[3*i+1], 
            s_GlobalMeshletBuffers[u_PC.MeshletBufferIndex].meshlets[mi].triangles[3*i+2]);
    }
}